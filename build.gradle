group = 'com.bettercloud'
version = '4.0.0'

ext.isReleaseVersion = !version.endsWith('SNAPSHOT')

if (!hasProperty('ossrhUsername')) {
    ext.ossrhUsername = ''
}
if (!hasProperty('ossrhPassword')) {
    ext.ossrhPassword = ''
}
if (!hasProperty('nexusUrl')) {
    ext.nexusUrl = ''
}
if (!hasProperty('nexusUsername')) {
    ext.nexusUsername = ''
}
if (!hasProperty('nexusPassword')) {
    ext.nexusPassword = ''
}

repositories {
    mavenLocal()
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    
    repositories {
        mavenCentral()
    }

    configurations.configureEach {
        exclude group: 'log4j'
        // see https://www.slf4j.org/codes.html#log4jDelegationLoop
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'slf4j-reload4j'
    }
}

subprojects {
    apply plugin: 'signing'
    
    group = rootProject.group
    archivesBaseName = project.name
    version = rootProject.version

    dependencies {
        constraints {
            implementation('net.minidev:json-smart:2.4.8') {
                because('CWE-400, CVE-2021-27568 - Denial of Service')
            }
        }

        // see https://www.slf4j.org/legacy.html
        implementation "org.slf4j:log4j-over-slf4j:$log4jOverSlf4jVersion"

        testImplementation "junit:junit:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set('javadoc')
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }
    
    artifacts {
        archives javadocJar, sourcesJar
    }
    
    signing {
        required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: nexusUrl) {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.project {
                    name 'BigTable ORM'
                    packaging 'jar'
                    description 'Configurable Java Entity Generation for Google BigTable'
                    url 'https://github.com/BetterCloud/bigtable-orm'

                    scm {
                        connection 'https://github.com/BetterCloud/bigtable-orm.git'
                        developerConnection 'https://github.com/BetterCloud/bigtable-orm.git'
                        url 'https://github.com/BetterCloud/bigtable-orm'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/BetterCloud/bigtable-orm/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'justindriggers'
                            name 'Justin Driggers'
                            email 'justin@justindriggers.com'
                        }
                    }
                }
            }
        }
    }
}

uploadArchives {
    dependsOn = subprojects.uploadArchives
    enabled = false
}

wrapper {
    gradleVersion = '5.2.1'
}